procedure
parm inp type=(string,99)
parm out type=(string,99)
parm scr_key type=(string,99)
parm xvd string valid=("n","y") default="n"

local temproot type=(string,99)
local fname3 type=(string,99)
local fname4 type=(string,99)

local ullon real
local ullat real
local urlon real
local urlat real
local lllon real
local lllat real
local lrlon real
local lrlat real
local inpnl int
local inpns int

body

! utility to convert ntm to geotiff

let temproot = "scratch/&scr_key"
let fname3 = "&temproot" // ".img"
let fname4 = "&temproot" // ".txt"

/*vextract &inp &temproot old call*/

vextract2 &inp &fname3
lab2asc &fname3 &fname4

! get the mapping info

asc2tcl &fname4 keyword="NITF_CornerLon1=" val=ullon vtype=8
asc2tcl &fname4 keyword="NITF_CornerLat1=" val=ullat vtype=8
asc2tcl &fname4 keyword="NITF_CornerLon2=" val=urlon vtype=8
asc2tcl &fname4 keyword="NITF_CornerLat2=" val=urlat vtype=8
asc2tcl &fname4 keyword="NITF_CornerLon3=" val=lrlon vtype=8
asc2tcl &fname4 keyword="NITF_CornerLat3=" val=lrlat vtype=8
asc2tcl &fname4 keyword="NITF_CornerLon4=" val=lllon vtype=8
asc2tcl &fname4 keyword="NITF_CornerLat4=" val=lllat vtype=8
asc2tcl &fname4 keyword="NITF_ROWS=" val=inpnl vtype=4
asc2tcl &fname4 keyword="NITF_COLUMNS=" val=inpns vtype=4

! gtgen the mapping in img file

gtgen inp=&fname3 'tiecnvrt 'add +
   geotiff=("ModelTiePointTag=(0,0,0,&ullon,&ullat,0.0)", +
  "ModelTiePointTag=(0,&inpnl,0,&lllon,&lllat,0.0)", +
  "ModelTiePointTag=(&inpns,0,0,&urlon,&urlat,0.0)", +
  "GTModelTypeGeoKey=2(ModelTypeGeographic)", +
  "GeogAngularUnitsGeoKey=9102(Angular_Degree)", +
  "GeographicTypeGeoKey=4326(GCS_WGS_84)", +
  "GTRasterTypeGeoKey=1(RasterPixelIsArea)", +
  "GeogEllipsoidGeoKey=7030(Ellipse_WGS84)")

! convert to external GeoTIFF

vtiff3-fromvic &fname3 &out

write "MSG: PROCESSING COMPLETED"

if (xvd="y") xvd &fname3

theend>
end-proc
