procedure help=*
parm key type=string
parm fname type=string
parm refname type=string
parm band type=int valid=(1,2,3,4,5,61,62,7)
parm line_upper int def=0
parm line_lower int def=0
parm maptype type=string valid=("pc","utm") default="pc"

local inloc type=string
local outloc type=string
local rawfile type=string
local tempfile0 type=string
local tempfile1 type=string
local tempfile2 type=string
local tempfile3 type=string
local gridfinal1 type=string
local gridfinal2 type=string
local gridfinal3 type=string
local gridinfo type=string
local masterfile type=string
local outfile type=string

local rsl real
local rss real
local rnl real
local rns real
local sl int
local ss int
local nl int
local ns int

local nlcopy int

local rslv real
local rssv real
local rnlv real
local rnsv real
local slv int
local ssv int
local nlv int
local nsv int

local t1l real
local t1s real
local t2l real
local t2s real
local t3l real
local t3s real
local t1e real
local t1n real
local t2e real
local t2n real
local t3e real
local t3n real

body


!  log the landsat band with vtiff3

let inloc = "raw" // "&key" // "/"
let outloc = "final" // "&key" // "/"
if (band=61 or band=62)
   let rawfile = "&inloc" // "&fname" // "_nn" // "&band" // ".tif"
else
   let rawfile = "&inloc" // "&fname" // "_nn" // "&band" // "0.tif"
end-if
let tempfile0 = "scratch/" // "xxx&key" // "temp0" // ".img"
let tempfile1 = "scratch/" // "xxx&key" // "temp1" // ".img"
let tempfile2 = "scratch/" // "xxx&key" // "temp2" // ".img"
let tempfile3 = "scratch/" // "xxx&key" // "temp3" // ".img"

if (band=61 or band=62)
   vtiff3-tovic inp=&rawfile out=&tempfile1
   gtsize &tempfile1 &tempfile0 pzoom=2
else
   vtiff3-tovic inp=&rawfile out=&tempfile0
end-if

! other filenames

let gridfinal1 = "final&key" // "/&refname"  // "gridfinal1"
let gridfinal2 = "final&key" // "/&refname"  // "gridfinal2"
let gridfinal3 = "final&key" // "/&refname"  // "gridfinal3"
let gridinfo = "final&key" // "/&refname"  // "gridinfo"
let outfile = "final&key" // "/&fname" // "_map4.band&band"

if (maptype="pc")
   let masterfile = "final&key" // "/&key" // "master.img"
else
   let masterfile = "final&key" // "/&key" // "master.img" // "u"
end-if

! get the parameters from the gridinfo file

ibis-l &gridinfo nr=4 'format
ibis2tcl &gridinfo v1=t1l v2=t1s v3=t1e v4=t1n +
    v5=t2l v6=t2s v7=t2e v8=t2n +
    v9=t3l v10=t3s v11=t3e v12=t3n +
    vartype=(-1,-1,-1,-1,-1,-1,-1,-1,-1,-1,-1,-1) +
    ibisloc=(1,1,1,2,1,3,1,4, +
             2,1,2,2,2,3,2,4, +
             3,1,3,2,3,3,3,4)
write "&t1l &t1s &t1e &t1n &t2l &t2s"
write "&t2e &t2n &t3l &t3s &t3e &t3n"
ibis2tcl &gridinfo v1=rsl v2=rss v3=rnl v4=rns +
   vartype=(0,0,0,0) ibisloc=(1,7,1,8,1,9,1,10)
let sl = $fix(rsl)
let ss = $fix(rss)
let nl = $fix(rnl)
let ns = $fix(rns)
write "sl ss nl ns &sl &ss &nl &ns"

! the sl,ss,nl,ns for geom is different

ibis2tcl &gridfinal2 v1=rslv v2=rssv v3=rnlv v4=rnsv +
   vartype=(-1,-1,-1,-1) ibisloc=(1,1,1,2,1,3,1,4)
let slv = $fix(rslv)
let ssv = $fix(rssv)
let nlv = $fix(rnlv)
let nsv = $fix(rnsv)
write "slv ssv nlv nsv &slv &ssv &nlv &nsv"

! shortened processing extracted from hywarp.pdf

let nlcopy = &line_lower-&line_upper+1
if (nlcopy=1) let nlcopy = 0
copy &tempfile0 &tempfile1 sl=&line_upper nl=&nlcopy
gtgen inp=&tempfile1 'tiecnvrt +
   geotiff=("ModelTiePointTag=(&t1s,&t1l,0,&t1e,&t1n,0.0)", +
  "ModelTiePointTag=(&t2s,&t2l,0,&t2e,&t2n,0.0)", +
  "ModelTiePointTag=(&t3s,&t3l,0,&t3e,&t3n,0.0)", +
  "GTModelTypeGeoKey=2(ModelTypeGeographic)", +
  "GTRasterTypeGeoKey=2(RasterPixelIsPoint)", +
  "GeogEllipsoidGeoKey=7030(Ellipse_WGS84)")
gtcopy &tempfile1 &tempfile2 size=(&sl,&ss,&nl,&ns)


geomv inp=(&tempfile2,&gridfinal1,&gridfinal3) +
      out=&tempfile3 size=(&slv,&ssv,&nlv,&nsv)
gtoffset &tempfile3 &masterfile    ! has to be whole pixel, verify (0,0)
gtsize (&tempfile3,&masterfile) &outfile 'coverref


theend>
end-proc
.TITLE
lswarpad - warp one additional band of landsat
.HELP
PURPOSE
     
	   
CALL
  
  
OPERATION


PERFORMANCE

Restrictions
------------


Original Programmer: A. L. Zobrist, 23 Jun, 2003
Current Cognizant Programmer: A. L. Zobrist

.LEVEL1
.vari key
the key chosen for the 
initial runs
.vari fname
the root filename
.vari refname
the filename used for band 3
in main procedure
.vari band
the band number
.vari maptype
"pc"  - output to Platte Caree
"utm" - output to UTM
.END
