procedure
! Jun 26, 2012 - RJB
! TEST SCRIPT FOR STRETCH
! tests BYTE, HALF images
!
! Vicar Programs:
!       gen list difpic concat size hist f2 qsar 
! 
! parameters:
!   <none>
!
! Requires NO external test data: 
refgbl $echo
refgbl $autousage
body
let _onfail="stop"
let $autousage = "no"
let $echo="no"
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!            ****Test for all functions on byte data ****                  !
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
write "!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!"
write "!            ****Test for all functions on byte data **** "
write "!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!"
let $echo="yes"
gen a 10 10			!Create a 10x10 byte image
list a

!.............Complementing the image........
stretch a b 'comp		!Test COMP options by comparing
stretch a c linear=(255,0)	!output with linear and
stretch a d table=(0,255,255,0) !table stretches.
difpic (b,c)			!Difference should be zero
difpic (b,d)			!Difference should be zero

!.............Linear stretches..........
stretch a b linear=(0,18)
list b
stretch a c perc=0 'include
difpic (b,c)			!Difference should be zero
histgen a h			!Compute histogram h
stretch (a,h) c perc=0 'include
difpic (b,c)			!Difference should be zero

!.............Piecewise linear histogram stretches........
stretch a c func="0.5*in1**2"	!Make the input image more interesting
list c
stretch c b 'mean 'include	!Put the mean in the middle
list b
stretch c b 'peak 'include	!Put the median in the middle
list b
stretch c b 'peak range=41
list b
stretch c b 'peak factor=2
list b

!.............Bimodal stretch (do not try this at home)........
stretch a b func="in1+18"	!Create image with bimodal histogram
concat (a,b) c ns=20 'nost
size c d zoom=10 'noin
stretch d e bimodal=0
hist e

!.............Bit clipping...............
stretch a b clip=6
list b
stretch a c func="mod(64*DN1,256)"
difpic (b,c)			!Difference should be zero

!.............Contouring...................
stretch a b contour=4 dnvalue=100
list b
stretch a c alarm=(0,4,8,12,16) dnvalue=100
difpic (b,c)			!Difference should be zero
stretch a c itable=(0,100,4,100,8,100,12,100,16,100)
difpic (b,c)			!Difference should be zero

!.............Logarithmic function...........
stretch a b log=(0,18)
stretch a c func="86.6039343332527*ln(IN1+1)"
difpic (b,c)			!Difference should be zero

!.............Sinusoidal function.............
stretch a b 'pstretch freq=16 phi=128 ampl=100 dc=49.5
list b
stretch a c func="(100/2)*sin((2*3.1415*16*dn1)/255+128)+49.5"
difpic (b,c)			!Difference should be zero
f2 a c func="(100/2)*sin((2*3.1415*16*dn1)/255+128)+49.5"
difpic (b,c)			!Difference should be zero

!................Histogram-matching stretches.....
stretch a b 'smooth
list b
stretch a b 'ellipse
list b
stretch a b power=1
list b
stretch a b 'gauss gsigma=5.0 'include
stretch b c func="dn+10"
stretch a b 'gauss gsigma=5.0 gmean=137.5 'include
difpic (b,c)		!Differences should be 0

!................Test histogram printing options..........
stretch a b 'ihist 'include
stretch a b 'ohist 'include
stretch a b 'icdf
stretch a b 'ocdf
let $echo="no"
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!            ****Test for all functions on halfword data ****              !
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
write "!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!"
write "!            ****Test for all functions on halfword data **** "
write "!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!"
let $echo="yes"
gen a 10 10 'half ival=-9	!Generate a 10x10 halfword image
list a

!.............Complementing the image........
stretch a b 'comp
list b
stretch a c linear=(32767,-32767)
difpic (b,c)				!Difference should be zero
stretch a c table=(-32767,32767,32767,-32767)
difpic (b,c)				!Difference should be zero
stretch a c func="-in1"
difpic (b,c)				!Difference should be zero

!.............Linear stretches..........
stretch a b linear=(-9,9)
list b
stretch a c perc=0 'include
difpic (b,c)			!Difference should be zero

!.............Piecewise linear histogram stretches........
gen c 10 10 'half
stretch c d func="0.5*in1**2"	!Make the input image more interesting
list d
stretch d b 'mean		!Put the mean in the middle
list b
stretch d b 'peak		!Put the median in the middle
list b

!.............Bimodal stretch (do not try this at home)........
stretch c d func="in1+18"	!Create image with bimodal histogram
concat (c,d) b ns=20 'nost
size b c zoom=10 'noin
stretch c d bimodal=0
hist d

!.............Contouring...............
stretch a b clip=6
list b
stretch a b contour=4 dnvalue=100
list b
stretch a c alarm=(-8,-4,0,4,8) dnvalue=100
difpic (b,c)                    !Difference should be zero
stretch a c itable=(-8,100,-4,100,0,100,4,100,8,100)
difpic (b,c)                    !Difference should be zero


!................Various functions...........
stretch a b log=(-9,9) curve=10
list b
stretch a b 'pstretch freq=4096 ampl=100
list b

!................Histogram-matching stretches.....
stretch a b 'smooth
list b
stretch a b 'gauss gsigma=2.
list b
stretch a b 'ellipse
list b
stretch a b power=1
list b

!................Test include/exclude/cut........................
stretch a b rexclude=(-9,-1) 'include perc=0.
list b
stretch a c iexclude=(-9,-8,-7,-6,-5,-4,-3,-2,-1) 'include perc=0.
difpic (b,c)		!Differences should be 0
stretch a b cut=10 'include
list b

!................Test area and linc parameters....................
stretch a b area=(2,2,8,8) perc=0.
list b
stretch a b linc=2 perc=0.
list b

!................Test SIZE parameter.....
stretch a b perc=0 size=(2,2,8,8) 'include
list b

!......SS+NS-1 Outside picture....samples trucated
stretch a b perc=0 ss=3 ns=11

!................Table stretches including neg values............
gen     out=a nl=3375 ns=3648 ival=-32768 linc=10 sinc=10 'half
hist    a

stretch a b table=(-32768,1.,1,10.,32767,10.) dnmin=-32768 dnmax=32767
hist    b

stretch  a c table=(-32768,-50.,-15000,50.,-14999,0.,0,0.,1,75.,32767,75.)
hist    c

stretch a d table=(-32768,-1000,-15000,-100,-1000,-10,0,0,1,10,32767,10) dnmin=-32768 dnmax=32767
hist    d

let $echo="no"
!
!
!
!  Testing 3D images
!
!
!
write "!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!"
write "!  Testing 3D images  "
write "!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!"
let $echo="yes"
gen a 10 10 10		!Create a 10x10X10 byte image
list a

!.............Complementing the image........
stretch a b 'comp		!Test COMP options by comparing
stretch a c linear=(255,0)	!output with linear and
stretch a d table=(0,255,255,0) !table stretches.
difpic (b,c)			!Difference should be zero
difpic (b,d)			!Difference should be zero

!.............Linear stretches..........
stretch a b linear=(0,18)
list b NB=2
stretch a c perc=0 'include
difpic (b,c)			!Difference should be zero


!.............Piecewise linear histogram stretches........
stretch a c func="0.5*in1**2"	!Make the input image more interesting
list c NB=2
stretch c b 'mean 'include	!Put the mean in the middle
list b NB=2
stretch c b 'peak 'include	!Put the median in the middle
list b NB=2
stretch c b 'peak range=41
list b NB=2
stretch c b 'peak factor=2
list b NB=2

!.............Bimodal stretch (do not try this at home)........
stretch a b func="in1+18"	!Create image with bimodal histogram



!.............Bit clipping...............
stretch a b clip=6
list b NB=2
stretch a c func="mod(64*DN1,256)"
difpic (b,c)			!Difference should be zero

!.............Contouring...................
stretch a b contour=4 dnvalue=100
list b NB=2
stretch a c alarm=(0,4,8,12,16) dnvalue=100
difpic (b,c)			!Difference should be zero
stretch a c itable=(0,100,4,100,8,100,12,100,16,100)
difpic (b,c)			!Difference should be zero

!.............Logarithmic function...........
stretch a b log=(0,18)
stretch a c func="86.6039343332527*ln(IN1+1)"
difpic (b,c)			!Difference should be zero

!.............Sinusoidal function.............
stretch a b 'pstretch freq=16 phi=128 ampl=100 dc=49.5
list b NB=2
stretch a c func="(100/2)*sin((2*3.1415*16*dn1)/255+128)+49.5"
difpic (b,c)			!Difference should be zero
f2 a c func="(100/2)*sin((2*3.1415*16*dn1)/255+128)+49.5"
difpic (b,c)			!Difference should be zero

!................Histogram-matching stretches.....
stretch a b 'smooth
list b NB=2
stretch a b 'ellipse
list b NB=2
stretch a b power=1
list b NB=2
stretch a b 'gauss gsigma=5.0 'include
stretch b c func="dn+10"
stretch a b 'gauss gsigma=5.0 gmean=137.5 'include
difpic (b,c)		!Differences should be 0

!................Test histogram printing options..........
stretch a b 'ihist 'include
stretch a b 'ohist 'include
stretch a b 'icdf
stretch a b 'ocdf
let $echo="no"
write "!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!"
write " test the case shown in AR 114525"
write "!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!"
let $echo="yes"
! test the case shown in AR 114525
gen xxb 11 7 ival=100 linc=0 sinc=0 'half
qsar xxb xxa area=(2 2 1 5 -32668 4 2 1 5 -100 6 2 1 5 2330 +
 8 2 1 5 3995 10 2 1 5 32667)
list xxa size=(1 1 11 7)
stretch xxa xxc table=(-32768 -32768 -1 -32768 0 0 1 1000 2429 1000 +
 2430 2430 2431 1000 4094 1000 4095 4095 4096 32767 32767 32767)
list xxc size=(1 1 11 7)

let $echo="no"
end-proc
